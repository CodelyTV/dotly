#!/usr/bin/env zsh

FIRST_CHARACTER_OK="▶"
FIRST_CHARACTER_KO="▶"
DIRTY="✗"
NONE="✓"

prompt_codelytv_pwd() {
  prompt_short_dir=$(dot filesystem short_pwd)
  print -n "%F{yellow}${prompt_short_dir}"
}

prompt_codelytv_git() {
  [[ -n ${git_info} ]] && print -n "%F{white} on${(e)git_info[prompt]}"
}

prompt_codelytv_precmd() {
  (( ${+functions[git-info]} )) && git-info
}

prompt_codelytv_setup() {
  local prompt_codelytv_status='%(?:%F{green}{%F{white}▸%F{green}}:%F{red}{%F{white}▸%F{red}})'

  autoload -Uz add-zsh-hook && add-zsh-hook precmd prompt_codelytv_precmd

  prompt_opts=(cr percent sp subst)

  zstyle ':zim:git-info:branch' format '%F{green}%b'
  zstyle ':zim:git-info:commit' format '%c'
  zstyle ':zim:git-info:clean' format '%F{white}$NONE'
  zstyle ':zim:git-info:dirty' format '%F{yellow}$DIRTY'
  zstyle ':zim:git-info:keys' format 'prompt' ' %F{cyan}%b%c %C%D'

  PS1="${prompt_codelytv_status} \$(prompt_codelytv_pwd)\$(prompt_codelytv_git)%f "
  RPS1=''
}

prompt_codelytv_setup "${@}"
